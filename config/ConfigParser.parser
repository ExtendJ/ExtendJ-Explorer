%header {: // this code is inlined before the generated parser
 package configAST;
:};

%embed {:
	static public class SyntaxError extends RuntimeException { public SyntaxError(String msg) {super(msg);}}
	// Disable syntax error recovery
	protected void recoverFromError(Symbol token, TokenStream in) {
		String error = "";
		error += Symbol.getLine(token.getStart());
		error += ',';
		error += Symbol.getColumn(token.getStart());
		error += '-';
		error += Symbol.getLine(token.getEnd());
		error += ',';
		error += Symbol.getColumn(token.getEnd());
		error += ": Syntax Error: unexpected token ";
		if (token.value != null)
		{
			error += '"';
			error += token.value;
			error += '"';
		}
		else
		{
			error += '#';
			error += token.getId();
		}
		throw new SyntaxError(error);
	}
:};

%goal debuggerconfig

DebuggerConfig debuggerconfig = configs.a block_list.b {: return new DebuggerConfig(new Opt(a), b); :}
		| block_list.a {: return new DebuggerConfig(new Opt(), a); :} ;

List block_list =
    block.a {: return new List(a); :}
    | block_list.c block.b {: return a.add(b); :};

Block block =
    include
    | global;

BindingList binding_list =
	/*EmptyList*/ {: return new BindingList(new List()); :}
	| binding_tmp.a SEMI {: return new BindingList(a); :};


List binding_tmp =
 	binding.d {: return new List().add(d); :}
 	| binding_tmp.c SEMI binding.d {: return c.add(d); :} ;

Binding binding = iddecl.a ASSIGNMENT value.b {: return new Binding(a,b); :};

Include include =
	INCLUDE.n LBRACKET RBRACKET {: return new Include(new ConfigDecl(n), new List()); :}
	| INCLUDE.n LBRACKET node_list.a RBRACKET {: return new Include(new ConfigDecl(n), a); :};

Global global =
    GLOBAL LBRACKET RBRACKET {: return new Global(new Opt(), new Opt()); :}
    | GLOBAL LBRACKET filter.a RBRACKET {: return new Global(new Opt(a), new Opt()); :}
    | GLOBAL LBRACKET configList.a RBRACKET {: return new Global(new Opt(), new Opt(a)); :}
    | GLOBAL LBRACKET filter.a configList.b RBRACKET {: return new Global(new Opt(a), new Opt(b)); :};

List node_list =
	node.e {: return new List().add(e); :}
	| node_list.f node.g {: return f.add(g); :} ;

NodeConfig node =
	node_name.a SEMI {: return new NodeConfig(a, new Opt(), new Opt()); :}
	| node_name.a LBRACKET RBRACKET {: return new NodeConfig(a, new Opt(), new Opt()); :}
	| node_name.a LBRACKET filter.c RBRACKET {: return new NodeConfig(a, new Opt(c), new Opt()); :}
	| node_name.a LBRACKET configList.c RBRACKET {: return new NodeConfig(a, new Opt(), new Opt(c)); :}
	| node_name.a LBRACKET filter.c configList.d RBRACKET {: return new NodeConfig(a, new Opt(c), new Opt(d)); :};

NodeName node_name =
	iddecl.a  {: return new NodeName(a, new Opt()); :}
	| iddecl.a COL iddecl.b {: return new NodeName(a, new Opt(b)); :};

ConfigList configList = configList_list.a {: return new ConfigList(a); :}

List configList_list  =
	configs.a {: return new List().add(a); :}
	| configList_list.a configs.b {: return a.add(b); :};

Configs configs =
	configdecl.a LBRACKET binding_list.h RBRACKET {: return new Configs(a, new Opt(h)); :};

Filter filter =
	FILTER LBRACKET filter_list.c RBRACKET {: return new Filter(c); :};

List filter_list =
		/*Empty*/{: return new List(); :}
		| filter_tmp.a SEMI {: return a; :};

List filter_tmp =
	binexpr.h {: return new List().add(h); :}
	| filter_tmp.i SEMI binexpr.j {: return i.add(j); :} ;

BinExpr binexpr =
	 value.a EQ value.b {:return new EQ(a,b); :}
	| value.a NEQ value.b {:return new NEQ(a,b); :}
	| value.a LEQ value.b  {:return new LEQ(a,b); :}
	| value.a GEQ value.b {:return new GEQ(a,b); :}
	| value.a LT value.b {:return new LT(a,b); :}
	| value.a GT value.b {:return new GT(a,b); :} ;

Value value =
	LSQBRACKET empty_list.a RSQBRACKET {: return a; :}
	| num
	| str
	| BOOL.a {: return new Bool(a); :}
	| iddecl
	| color;

Value empty_list =
	/*EmptyList*/ {: return new EmptyArray(); :}
	| str_list.a {: return new StrArray(a); :}
	| num_list.a {: return new NumArray(a); :};

List str_list =
	 str.l {: return new List().add(l); :}
	| str_list.m COMMA str.n {: return m.add(n); :} ;

List num_list =
	 num.o {: return new List().add(o); :}
	| num_list.p COMMA num.q {: return p.add(q); :} ;

Num num =  NUMERAL.a {: return new Num(a); :};
Str str = STRING.a {: return new Str(a); :};
IdDecl iddecl = ID.a {: return new IdDecl(a); :};
ConfigDecl configdecl = IDCONFIG.a {: return new ConfigDecl(a); :};
Color color = COLOR.a {: return new Color(a); :};

